// Lexical actions for English
// as per Dynamics of Conversational Dialogue project, 2009

// Rule metavariables: uppercase rule spec arguments
// Formula metavariables: A,B,C
// Bottom requirement: !

proper(NAME,PERSON,CLASS)
IF      ?ty(e)
THEN    put(person(PERSON))
        put(class(CLASS))
        make(\/1)
        go(\/1)
        put(ty(cn>e))
        put(fo(x^(iota, x)))
        go(/\)
        make(\/0)
        go(\/0)
        put(?ty(cn))
        make(\/0)
        go(\/0)
        put(ty(e))
        freshput(S,entity)
        go(/\)
        make(\/1)
        go(\/1)
        put(ty(e>cn))
        put(fo(y^y, NAME(y)))
        go(/\)
ELSE    abort

det_quant(QUANT)
IF      ?ty(e)
THEN   	make(\/1)
        go(\/1)
        put(fo(x^(QUANT, x)))
        put(ty(cn>e))
        put(!)
        go(/\1)
        make(\/0)
        go(\/0)
        put(?ty(cn))
ELSE    abort


mass(NAME,PERSON,CLASS)
IF      ?ty(e)
THEN    put(ty(e))
        put(fo(NAME))
        put(person(PERSON))
        put(class(CLASS))
        put(!)
ELSE    abort

count(PRED,PERSON,CLASS)
IF      ?ty(cn)
		//</\0>?ty(e)
THEN    make(\/0)
        go(\/0)
        freshput(S,entity)
        put(ty(e))      
        put(!)
        go(/\0)
        make(\/1)
        go(\/1)
        put(fo(x^x,PRED(x)))
        put(ty(e>cn))
        put(!)
        go(/\1)
        go(\/0)
ELSE    abort

adj(PRED)
IF      ?ty(cn)
THEN    make(\/1)
        go(\/1)
        put(fo(x^PRED(x)))
        put(ty(cn>cn))
        put(!)
        go(/\1)
        make(\/0)
        go(\/0)
        put(?ty(cn))
ELSE    abort

pron_nom(PERSON,CLASS)
IF      ?ty(e)
        </\U>?ty(t)
THEN    put(ty(e))
        //* put(fo(A)) // formula metavariables not working with generation *//
        put(?Ex.fo(x))
        put(person(PERSON))
        put(class(CLASS))
        put(!)
ELSE    abort

pron_acc(PERSON,CLASS)
IF      ?ty(e)
        </\0>?ty(e>X)
THEN    put(ty(e))
        //* put(fo(A)) // formula metavariables not working with generation *//
        put(?Ex.fo(x))
        put(person(PERSON))
        put(class(CLASS))
        put(!)
ELSE    abort

pron_ref(PERSON,CLASS)
IF		?ty(e)
		¬</\0>?ty(es>t)
		</\0/\*\/0>(ty(e)& fo(X)& class(CLASS)& person(PERSON))
THEN	put(fo(X))
		put(ty(e))
		put(person(PERSON))
		put(class(CLASS))
		put(!)
ELSE	abort

pron_whrel(CLASS)
IF      ?ty(e)
        [/\*/\L]fo(X)
THEN    put(ty(e))
        put(fo(X))
ELSE    abort

//*
pron_whq(CLASS)
IF		?ty(e)
		</\*>?ty(t)
THEN	put(ty(e))
		put(fo(A))
		put(class(CLASS))
ELSE	abort
*//

v_intran_inf(PRED,TENSE,PERSON)
IF      ty(e>(es>t))
        ?Ex.fo(x)
        </\1/\1\/U>Ex.x
        </\1/\1\/U>ty(e)
        ¬<\/0>Ex.x
        ¬<\/1>Ex.x        
THEN    delete(?Ex.fo(x))
		go(</\1\/0>)
		merge(</\0/\1\/U>)
		delete(?ty(e))
		delete(?Ex.tn(x))
		go(</\0\/1>)
		v_intran_content(PRED)
		go(</\0/\1\/0\/0\/1\/1>)
		conjoin(x^y^y, (y subset x))
		go(</\1/\1/\0/\0\/1\/0>)
ELSE    abort

v_intran_fin(PRED,TENSE,PERSON)
IF		?ty(t)
        <\/U>Ex.x
        <\/U>ty(e)
        ¬<\/1>Ex.x
		¬<\/0>Ex.x
THEN	make(\/0)
		go(\/0)
		put(?ty(es))
		build_event_term(TENSE)
		go(</\1/\1/\0/\0>)
		make(\/1)
		go(\/1)
		put(?ty(es>t))
		make(\/0)
		go(\/0)
		put(?ty(e))
		go(/\0)
		make(\/1)
		go(\/1)
		put(ty(e>(es>t)))
		put(?Ex.fo(x))		
ELSE	abort
IF		ty(e>(es>t))
        ?Ex.fo(x)
        </\1/\1\/U>Ex.x
        </\1/\1\/U>ty(e)
        ¬<\/0>Ex.x
        ¬<\/1>Ex.x
THEN	delete(?Ex.fo(x))
		go(</\1\/0>)
		merge(</\0/\1\/U>)
		delete(?ty(e))
		delete(?Ex.tn(x))
		go(</\0\/1>)
		v_intran_content(PRED)
		go(</\0/\1\/0\/0\/1\/1>)
		conjoin(x^y^y, (y subset x))
		go(</\1/\1/\0/\0\/1\/0>)
ELSE	abort  


v_tran_inf(PRED,TENSE,PERSON)
IF      ty(e>(es>t))
        ?Ex.fo(x)
        </\1/\1\/U>Ex.x
        </\1/\1\/U>ty(e)
        ¬<\/0>Ex.x
        ¬<\/1>Ex.x      
THEN    delete(?Ex.fo(x))
		delete(ty(e>(es>t)))
		put(?ty(e>(es>t)))
		go(</\1\/0>)
		merge(</\0/\1\/U>)
		delete(?ty(e))
		delete(?Ex.tn(x))
		go(</\0\/1>)
		v_tran_content(PRED)
		go(</\0/\1/\1\/0\/0\/1\/1>)
		conjoin(x^y^y, (y subset x))
		go(</\1/\1/\0/\0\/1\/0>)
ELSE    abort

v_tran_fin(PRED,TENSE,PERSON)
IF		?ty(t)
		tn(X)
		<\/U>Ex.x
		<\/U>ty(e)
		¬<\/1>Ex.x
		¬<\/0>Ex.x
THEN	make(\/0)
		go(\/0)
		put(?ty(es))
		build_event_term(TENSE)
		go(</\1/\1/\0/\0>)
		make(\/1)
		go(\/1)
		put(?ty(es>t))
		make(\/0)
		go(\/0)
		put(?ty(e))
		go(/\0)
		make(\/1)
		go(\/1)
		put(ty(e>(es>t)))
		put(?Ex.fo(x))
ELSE	abort
IF      ty(e>(es>t))
        ?Ex.fo(x)
        </\1/\1\/U>Ex.x
        </\1/\1\/U>ty(e)
        ¬<\/0>Ex.x
        ¬<\/1>Ex.x      
THEN    delete(?Ex.fo(x))
		delete(ty(e>(es>t)))
		put(?ty(e>(es>t)))
		go(</\1\/0>)
		merge(</\0/\1\/U>)
		delete(?ty(e))
		delete(?Ex.tn(x))
		go(</\0\/1>)
		v_tran_content(PRED)
		go(</\0/\1/\1\/0\/0\/1\/1>)
		conjoin(x^y^y, (y subset x))
		go(</\1/\1/\0/\0\/1\/0>)
ELSE    abort
		
v_aux_inf(PRED,TENSE,PERSON)
IF		?ty(t)
THEN	IF 		<\/1>Ex.x
		   		<\/0>Ex.x
		THEN	abort
		ELSE	IF		<\/U>Ex.x
				THEN 	aux_content(PRED)
				ELSE	put(+Q)
						aux_content(PRED)
						go(</\1/\1>)
						make(\/U)
						go(\/U)
						put(?ty(e))
						put(?Ex.tn(x))
ELSE	abort

v_aux_fin(PRED,TENSE,PERSON)
IF		?ty(t)
THEN	IF 		<\/1>Ex.x
		   		<\/0>Ex.x
		THEN	abort
		ELSE	IF		<\/U>Ex.x
				THEN 	aux_content(PRED)
				ELSE	put(+Q)
						aux_content(PRED)
						go(</\1/\1>)
						make(\/U)
						go(\/U)
						put(?ty(e))
						put(?Ex.tn(x))
ELSE	abort
